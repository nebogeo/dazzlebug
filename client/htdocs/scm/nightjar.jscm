;; -*- mode: scheme; -*-
;; Little Canvas Engine Copyright (C) 2013 Dave Griffiths
;;
;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU Affero General Public License as
;; published by the Free Software Foundation, either version 3 of the
;; License, or (at your option) any later version.
;;
;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU Affero General Public License for more details.
;;
;; You should have received a copy of the GNU Affero General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

;; a simple 2D game engine for making fast html5 canvas games

;; --------------------------------------------
;; server comms

(js "$.ajaxSetup ({ cache: false })")

  ;; hack together a js object to send
(define (build-url-args args)
  (foldl
   (lambda (i r) (js "r[i[0]]=i[1]") r)
   (js "{}") args))

(define (server-call name argsl)
  (let ((args (build-url-args argsl)))
    (set! args.fn name)
    (let ((v ($.get "egglab" args)))
      (v.fail (lambda (jqXHR textStatus errorThrown)
                (msg textStatus)
                (msg errorThrown))))))

(define (server-call-mutate name argsl f)
  (let ((args (build-url-args argsl)))
    (set! args.fn name)
    (let ((v ($.get "egglab" args (mutate-game f))))
      (v.fail (lambda (jqXHR textStatus errorThrown)
                (msg textStatus)
                (msg errorThrown))))))

;; --------------------------------------------
;; basics

(define (transform x y r s) (list x y r s))

(define (transform-x t) (list-ref t 0))
(define (transform-y t) (list-ref t 1))

(define (time-to-seconds t)
  (/ (Math.floor (/ t 10)) 100))

(define image-lib ())
(define sound-lib ())

(define (load-image-mutate fn filename)
  (let ((image (js "new Image()")))
    (set! image.onload
          (mutate-game
           (lambda (c)
             (set! image-lib (cons (list filename image) image-lib))
             (fn c))))
    (set! image.src (+ "images/" filename))))

(define (load-image! fn finished images)
  (let ((image (js "new Image()")))
    (set! image.onerror
          (lambda ()
            (msg (+ "error loading:" fn))))
    (set! image.onload
          (lambda ()
            (set! image-lib (cons (list fn image) image-lib))
            (ctx.clearRect 0 0 screen-width screen-height)
            (centre-text ctx "Loading..." 0 240)
            (centre-text
             ctx
             (+ "" (Math.floor (* (/ (length image-lib)
                                     (length images)) 100)) "%") 0 340)
            (when (eq? (length image-lib)
                       (length images))
                  (finished))))
    (set! image.src (+ "images/" fn))))

(define (load-images! l finished)
  (for-each
   (lambda (fn)
     (load-image! fn finished l))
   l))

(define (load-sounds! l)
  (for-each
   (lambda (fn)
     (set! sound-lib
           (cons (list fn (new Audio (string-append "sounds/" fn)))
                 sound-lib)))
   l))

(define (inner_find-image fn l)
  (cond
   ((null? l) #f)
   ((eq? (car (car l)) fn) (cadr (car l)))
   (else (inner_find-image fn (cdr l)))))

(define (find-image fn)
  (inner_find-image fn image-lib))

(define (inner_find-sound fn l)
  (cond
   ((null? l) #f)
   ((eq? (car (car l)) fn) (cadr (car l)))
   (else (inner_find-sound fn (cdr l)))))

(define (find-sound fn)
  (inner_find-sound fn sound-lib))

(define (play-sound sound)
  (when (game-mute game)
        (let ((snd (find-sound sound)))
          (snd.play))))

(define (play-sound-looped sound)
  (when (game-mute game)
        (let ((snd (find-sound sound)))
          (if (eq? (js "typeof snd.loop") "boolean")
              (set! snd.loop true)
              (snd.addEventListener
               "ended" (lambda ()
                         (set! snd.currentTime 0)
                         (snd.play)) #f))
          (snd.play))))

(define (stop-sound sound)
  (when (game-mute game)
        (let ((snd (find-sound sound)))
          (snd.pause))))

;; ----------------------------------------
;; drawing helpers

(define (fill-centre-text ctx txt x y)
  (let ((m (ctx.measureText txt)))
    (ctx.fillText txt (- x (/ m.width 2)) y)))

(define (centre-text ctx txt x y)
  (let ((m (ctx.measureText txt)))
    (ctx.fillText txt (+ x (- (/ screen-width 2) (/ m.width 2))) y)))

(define (wrap-text ctx text x y max-width line-height)
  (define (wrap-text-inner words line y)
    (cond
     ((null? words)
      (centre-text ctx line x y))
     (else
      (let ((test-line (+ line (car words) " ")))
        (let ((metrics (ctx.measureText test-line)))
          (cond ((> metrics.width max-width)
                 (centre-text ctx line x y)
                 (wrap-text-inner (cdr words)
                                  (+ (car words) " ")
                                  (+ y line-height)))
                (else
                 (wrap-text-inner
                  (cdr words) test-line y))))))))
  (wrap-text-inner (text.split " ") "" y))

(define (sprite x y image timer)
  (list x y image timer))

(define (sprite-x s) (list-ref s 0))
(define (sprite-y s) (list-ref s 1))
(define (sprite-modify-x s v) (list-replace s 0 v))
(define (sprite-modify-y s v) (list-replace s 1 v))
(define (sprite-image s) (list-ref s 2))
(define (sprite-timer s) (list-ref s 3))

(define (sprite-render ctx t s)
  (when (< t (sprite-timer s))
        (ctx.save)
        (ctx.translate (sprite-x s) (sprite-y s))
        (ctx.drawImage
         (find-image (sprite-image s))
         0 0)
        (ctx.restore)))

;; ----------------------------------------
;; button stuff

(define (rect-button name x y w h jitter callback)
      (list "rect-button" name x y w h
            jitter callback #f (rndf) #f))

(define (image-button name x y jitter drawable callback)
  (list "image-button"
        name
        (- x (/ drawable.width 2))
        (- y (/ drawable.height 2))
        drawable.width
        drawable.height
        jitter callback drawable
        (rndf)
        #f))

(define (animated-button name x y dir jitter drawable animate-callback callback)
  (list "animated-button"
        name
        (- x (/ drawable.width 2))
        (- y (/ drawable.height 2))
        drawable.width
        drawable.height
        jitter callback drawable
        (rndf)
        #f
        dir
        animate-callback
        0
        ""))

(define (button-modify-x b v) (list-replace b 2 v))
(define (button-modify-y b v) (list-replace b 3 v))
(define (animated-button-dir b) (list-ref b 11))
(define (animated-button-modify-dir b v) (list-replace b 11 v))
(define (animated-button-callback b) (list-ref b 12))
(define (animated-button-time b) (list-ref b 13))
(define (animated-button-modify-time b v) (list-replace b 13 v))
(define (animated-button-state b) (list-ref b 14))
(define (animated-button-modify-state b v) (list-replace b 14 v))

(define (circle-button name x y r callback)
  (list "circle-button" name x y r r #f callback #f (rndf) #f))

(define (button-type b) (list-ref b 0))
(define (button-name b) (list-ref b 1))
(define (button-x b) (list-ref b 2))
(define (button-y b) (list-ref b 3))
(define (button-w b) (list-ref b 4))
(define (button-r b) (list-ref b 4))
(define (button-h b) (list-ref b 5))
(define (button-jitter b) (list-ref b 6))
(define (button-callback b) (list-ref b 7))
(define (button-drawable b) (list-ref b 8))
(define (button-modify-drawable b v) (list-replace b 8 v))
(define (button-offs b) (list-ref b 9))
(define (button-mouse-over b) (list-ref b 10))
(define (button-modify-mouse-over b v) (list-replace b 10 v))


(define (dist-2d x1 y1 x2 y2)
  (let ((x (- x2 x1))
        (y (- y2 y1)))
    (Math.sqrt (+ (* x x) (* y y)))))

(define (in-rect? x y w h xx yy)
  (and (> xx x)
       (< xx (+ x w))
       (> yy y)
       (< yy (+ y h))))

(define (in-circle? x y r xx yy)
  (< (dist-2d xx yy x y) r))

;; fluff for fancy text rendering

(define (button-do-jittering b t)
  (when (eq? (button-jitter b) "rotate")
        (ctx.translate (/ (button-w b) 2)
                       (/ (button-h b) 2))
        (ctx.rotate (* 0.05 (Math.sin (+ (* (button-offs b) 10) (* t 0.01)))))
        (ctx.translate (- 0 (/ (button-w b) 2))
                       (- 0 (/ (button-h b) 2))))
  (when (eq? (button-jitter b) "slidy")
        (ctx.translate (* 2.5 (sin (+ (* (button-offs b) 10) (* t 0.004)))) 0)))


(define (button-text-jumpy-render! b t)
  (set! ctx.font "normal 15pt 'Press Start 2P'")
  (set! ctx.fillStyle "#000")
  (let ((m (ctx.measureText (button-name b)))
        (cm (ctx.measureText "w"))
        (pos 0)
        (txt (button-name b)))
    (let ((ta (txt.split ""))
          (x (- (/ (button-w b) 2) (/ m.width 2)))
          (y (+ (/ (button-h b) 2) 5)))
      (index-for-each
       (lambda (i c)
         (set! cm (ctx.measureText c))
         (ctx.fillText c (+ x pos)
                       (+ y (* 3 (sin (+ (* t 0.01) i)))))
         (set! pos (+ pos cm.width)))
       ta))))

(define (to-hex c)
  (let ((c (Math.floor (Math.abs c))))
    (let ((hex (c.toString 16)))
      (if (eq? hex.length 1)
          (+ "0" hex)
          hex))))

(define (rgb-to-hex r g b)
  (+ "#" (to-hex r) (to-hex g) (to-hex b)))

(define (rgba-to-hex r g b a)
  (+ "#" (to-hex r) (to-hex g) (to-hex b) (to-hex a)))

(define (text-flashy-render ctx txt x y t)
  (let ((m (ctx.measureText txt))
        (cm 0)
        (pos 0))
    (let ((ta (txt.split "")))
      (index-for-each
       (lambda (i c)
         (let ((col (* 128 (sin (+ (* t 0.01) i)))))
           (set! ctx.fillStyle (rgb-to-hex col col col)))
         (set! cm (ctx.measureText c))
         (ctx.fillText c (+ x pos) y)
         (set! pos (+ pos cm.width)))
       ta))))

(define (button-text-flashy-render! b t)
  (let ((m (ctx.measureText (button-name b)))
        (cm (ctx.measureText "w"))
        (txt (button-name b))
        (pos 0))
    (let ((ta (txt.split ""))
          (x (- (/ (button-w b) 2) (/ m.width 2)))
          (y (+ (/ (button-h b) 2) 5)))
      (index-for-each
       (lambda (i c)
         (set! ctx.fillStyle (rgb-to-hex
                              (* 256 (sin (+ (* t 0.001) i)))
                              (* 256 (sin (+ (* t 0.003) i)))
                              (* 256 (sin (+ (* t 0.004) i)))))
         (set! cm (ctx.measureText c))
         (set! pos (+ pos cm.width))
         (ctx.fillText c (+ x pos) y))
       ta))))

(define (button-text-render! b t)
  (cond
   ((eq? (button-jitter b) "jumpy") (button-text-jumpy-render! b t))
   ((eq? (button-jitter b) "flashy") (button-text-flashy-render! b t))
   (else
    (let ((m (ctx.measureText (button-name b))))
      (ctx.fillText
       (button-name b)
       (- (/ (button-w b) 2) (/ m.width 2))
       (+ (/ (button-h b) 2) 5))))))


(define (rect-button-update! b mx my c)
  (if (in-rect? (button-x b) (button-y b)
                (button-w b) (button-h b)
                mx my)
      (let ((fn (button-callback b)))
        (list #t (fn c)))
      (list #f c)))

(define (circle-button-update! b mx my c)
  (if (in-circle? (button-x b) (button-y b)
                  (button-r b) mx my)
      (let ((fn (button-callback b)))
        (list #t (fn c)))
      (list #f c)))

(define (button-update! b e mx my c)
  (if (eq? e "move")
      (let ((fn (button-mouse-over b)))
        (if (and fn (in-rect? (button-x b) (button-y b)
                              (button-w b) (button-h b)
                              mx my))
            (list #t (fn c))
            (list #f c)))
      (cond
       ((eq? (button-type b) "rect-button")
        (rect-button-update! b mx my c))
       ((eq? (button-type b) "image-button")
        (rect-button-update! b mx my c))
       ((eq? (button-type b) "animated-button")
        (rect-button-update! b mx my c))
       (else
        (circle-button-update! b mx my c)))))

(define (rect-button-render! ctx t b)
  (when #f
        (ctx.save)
        (ctx.translate (button-x b) (button-y b))
        (button-do-jittering b t)
        (set! ctx.strokeStyle "#000")
        (ctx.strokeRect
         0 0 (button-w b) (button-h b))
        (ctx.fillText (button-name b) (/ (button-w b) 2) (+ 20 (/ (button-h b) 2)))
        (ctx.restore)
        ))

(define (image-button-render! ctx t b)
  (ctx.save)
  (ctx.translate (button-x b) (button-y b))
  (button-do-jittering b t)
  (ctx.drawImage
   (button-drawable b)
   0 0)
  (button-text-render! b t)
  (ctx.restore))

(define (animated-button-render! ctx t b)
  (ctx.save)

  (ctx.translate (button-x b) (button-y b))

  (ctx.translate 64 64)
  (ctx.rotate (Math.atan2 (vy (animated-button-dir b))
                          (vx (animated-button-dir b))))
  (ctx.rotate Math.PI/2)
  (ctx.translate -64 -64)

  (button-do-jittering b t)

  (ctx.drawImage
   (button-drawable b)
   0 0)
  (button-text-render! b t)
  (ctx.restore))


(define (circle-button-render! ctx t b)
  (ctx.beginPath)
  (ctx.arc (button-x b) (button-y b)
           (button-r b) 0 (* Math.PI 2) true)
  (ctx.closePath)
  (set! ctx.strokeStyle "#000")
  (ctx.stroke))

(define (button-render! ctx t b)
  (cond
   ((eq? (button-type b) "rect-button")
    (rect-button-render! ctx t b))
   ((eq? (button-type b) "animated-button")
    (animated-button-render! ctx t b))
   ((eq? (button-type b) "image-button")
    (image-button-render! ctx t b))
   (else
    (circle-button-render! ctx t b))))

;; ----------------------------------------

(define (button-inner-update b e mx my c)
  (foldl
   (lambda (b r)
     (if (not (car r)) ;; if event not handled
         (button-update! b e mx my (cadr r))
         (js "r")))
   (list #f c)
   b))

(define (buttons-update b e mx my c)
  (button-inner-update b e mx my c))

(define (buttons-render! ctx t b)
  (for-each
   (lambda (b)
     (button-render! ctx t b))
   (reverse b)))

;; ----------------------------------------
;; draw a clock

(define (stroke-clock ctx x y time max-time)
  (ctx.beginPath)
  (ctx.moveTo x y)
  (ctx.arc
   x y 50 (* Math.PI -0.5)
   (+ (* Math.PI -0.5)
      (/ (* time Math.PI 2) max-time))
   true)
  (ctx.closePath))

(define (nightjar-draw-clock ctx time max-time)
  (set! ctx.lineWidth 4)
  (set! ctx.strokeStyle "#000")
  (stroke-clock ctx 74 74 time max-time)
  (ctx.stroke)

  (stroke-clock ctx 70 70 time max-time)
  (set! ctx.fillStyle "rgba(1, 0, 0, 0.7)")
  (ctx.fill)
  (set! ctx.lineWidth 4)
  (set! ctx.strokeStyle "#777")
  (ctx.stroke)

  (set! ctx.lineWidth 1)
  (set! ctx.fillStyle "#fff")
  (set! ctx.font "normal 30pt gnuolane")
  (ctx.fillText (Math.floor (- max-time time)) 54 110))

;; ----------------------------------------
;; main game structure

(define (make-new-game)
  (list 0 (lambda (ctx) 0) (lambda (t c) c)
        () 0 0 () (list 0 0) (lambda (e c) c)
        (lambda (c) c) (lambda (ctx) 0) 1 0
	(lambda () 0)))

(define (game-time g) (list-ref g 0))
(define (game-modify-time v g) (list-replace g 0 v))
(define (game-render g) (list-ref g 1))
(define (game-modify-render v g) (list-replace g 1 v))
(define (game-update g) (list-ref g 2))
(define (game-modify-update v g) (list-replace g 2 v))
(define (game-buttons g) (list-ref g 3))
(define (game-modify-buttons v g) (list-replace g 3 v))
(define (game-data g) (list-ref g 4))
(define (game-modify-data fn g) (list-replace g 4 (fn (game-data g))))
(define (game-mx g) (list-ref g 5))
(define (game-modify-mx v g) (list-replace g 5 v))
(define (game-my g) (list-ref g 6))
(define (game-modify-my v g) (list-replace g 6 v))
(define (game-world-offset g) (list-ref g 7))
(define (game-modify-world-offset g v) (list-replace g 7 v))
(define (game-mouse-hook g) (list-ref g 8))
(define (game-modify-mouse-hook v g) (list-replace g 8 v))
(define (game-over-nothing-hook g) (list-ref g 9))
(define (game-modify-over-nothing-hook v g) (list-replace g 9 v))
(define (game-postrender g) (list-ref g 10))
(define (game-modify-postrender v g) (list-replace g 10 v))
(define (game-mute g) (list-ref g 11))
(define (game-modify-mute g v) (list-replace g 11 v))
(define (game-delta g) (list-ref g 12))
(define (game-modify-delta g v) (list-replace g 12 v))
(define (game-timeout g) (list-ref g 13))
(define (game-modify-timeout v g) (list-replace g 13 v))

(define (game-flip-mute g) (game-modify-mute g (if (zero? (game-mute g)) 1 0)))

(define (game-modify-mouse g x y)
  (game-modify-mx
    x (game-modify-my y g)))

(define (game-mouse-down g mx my)
  (let ((fn (game-mouse-hook g)))
    (fn
     "down"
     (cadr
      (buttons-update (game-buttons g) "down" mx my
                      (game-modify-mouse g mx my))))))

(define (game-mouse-move g mx my)
  (let ((ret (buttons-update
              (game-buttons g) "move" mx my
              (game-modify-mouse g mx my)))
        (over-nothing (game-over-nothing-hook g)))
    (if (not (car ret))
        (over-nothing g)
        g)))

(define (game-mouse-up g mx my)
  (game-modify-mouse g mx my))

;; ----------------------------------------

(define (button-bounce b)
  (let ((d (animated-button-dir b)))
    (cond
     ((< (button-x b) 0)
      (button-modify-x
       (animated-button-modify-dir
        b (vector (- 0 (vx d)) (vy d)))
       0))
     ((< (button-y b) 0)
      (button-modify-y
       (animated-button-modify-dir
        b (vector (vx d) (- 0 (vy d))))
       0))
     ((> (+ (button-x b) (button-w b)) screen-width)
      (button-modify-x
       (animated-button-modify-dir
        b (vector (- 0 (vx d)) (vy d)))
       (- screen-width (button-w b))))
     ((> (+ (button-y b) (button-w b)) screen-height)
      (button-modify-y
       (animated-button-modify-dir
        b (vector (vx d) (- 0 (vy d))))
       (- screen-height (button-h b))))
     (else b))))

(define (game-animate-buttons game)
  (game-modify-buttons
   game
   (map
    (lambda (b)
      (if (eq? (button-type b) "animated-button")
          (let ((cb (animated-button-callback b)))
            (cb b))
          b))
    (game-buttons game))))

(define timeout-trigger (* 2 60))
(define timeout-time 0)

(define (top-update-game t game)
  (let ((fn (game-update game)))
    (set! timeout-time (+ timeout-time (game-delta game)))
    (when (> timeout-time timeout-trigger)
          (let ((fn (game-timeout game)))
            (fn)))
    (set! game
          (game-animate-buttons
           (game-modify-time
            t (game-modify-delta
               (fn t game) (/ (- t (game-time game)) 1000.0)))))))

(define (top-render-game ctx game)
  (let ((fn (game-render game)))
    (fn ctx)))

(define (top-postrender-game ctx game)
  (let ((fn (game-postrender game)))
    (fn ctx)))

(define clear-screen 1)

(define (top-render)
  (when (not (eq? game 0))
        ;; wrap all game code in a exception handler
        (js "try {")
        (when (eq? clear-screen 1)
              (ctx.clearRect 0 0 screen-width screen-height))
        (let ((t (- (js "new Date()") load-time)))
          (set! ctx.fillStyle "#000");
;;          (ctx.fillText (+ "Time is: " t) 10 750)
          (top-update-game t game)

          (ctx.save)

          (top-render-game ctx game)
          (ctx.translate (car (game-world-offset game))
                         (cadr (game-world-offset game)))

          (buttons-render! ctx t (game-buttons game))

          (ctx.restore)
          (top-postrender-game ctx game)

          )
        (js "} catch(e) {")
        ;; not sure why I need to catch this to show stack trace...???
        (msg e)
        ;; and this to stop execution...
        (js "throw new Error();")
        (js "}")
        (requestAnimFrame top-render ctx)))

(define game 0)

(define (mouse-from-event g canvas e)
  (set! timeout-time 0)
  (let ((rect (canvas.getBoundingClientRect)))
    (let ((sx (/ rect.width screen-width))
          (sy (/ rect.height screen-height)))
      (list (- (/ (- e.clientX rect.left) sx)
               (car (game-world-offset g)))
            (- (/ (- e.clientY rect.top) sy)
               (cadr (game-world-offset g)))))))

(define (touch-from-event g canvas e)
  (set! timeout-time 0)
  (let ((e (car e.targetTouches)))
    (mouse-from-event g canvas e)))

(define touchscreen 0)

(define (start-game canvas ctx)
  (ctx.clearRect 0 0 screen-width screen-height)

  (canvas.addEventListener
   "mousedown"
   (lambda (e)
     (when (zero? touchscreen)
           (let ((m (mouse-from-event game canvas e)))
             (set! game (game-mouse-down game (car m) (cadr m)))
             ))))

  (canvas.addEventListener
   "mousemove"
   (lambda (e)
     (let ((m (mouse-from-event game canvas e)))
       (set! game (game-mouse-move game (car m) (cadr m)))
       )))

  (canvas.addEventListener
   "mouseup"
   (lambda (e)
     (let ((m (mouse-from-event game canvas e)))
       (set! game (game-mouse-up game (car m) (cadr m)))
       )))

  (canvas.addEventListener
   "touchstart"
   (lambda (e)
     (e.preventDefault)
     (set! touchscreen 1)
     (let ((m (touch-from-event game canvas e)))
       (set! game (game-mouse-down game (car m) (cadr m))))))

  (canvas.addEventListener
   "touchmove"
   (lambda (e)
     (e.preventDefault)
     (let ((m (touch-from-event game canvas e)))
       (set! game (game-mouse-move game (car m) (cadr m))))))

  (canvas.addEventListener
   "touchend"
   (lambda (e)
     (e.preventDefault)
     (set! game (game-mouse-up game 0 0))))

  (canvas.addEventListener
   "touchcancel"
   (lambda (e)
     (e.preventDefault)
     (set! game (game-mouse-up game 0 0))))

  ;; todo - pass in game specific func
  (set! game (nightjar-intro (make-new-game)))
  (requestAnimFrame top-render ctx))


(define (mutate-game f)
  (lambda (data)
    (set! game (f game data))))

;; drawing utils (remove?)

(define (nightjar-heading ctx txt)
  (set! ctx.fillStyle "#000")
  (ctx.save)
  (ctx.translate 4 4)
  (wrap-text ctx txt 0 200 1000 100)
  (ctx.restore)
  (set! ctx.fillStyle "#000")
  (wrap-text ctx txt 0 200 1000 100))

(define (nightjar-text ctx txt)
  (wrap-text ctx txt 0 400 1000 50))

(define (nightjar-all-text ctx txt)
  (wrap-text ctx txt 0 200 1000 75))


;; ----------------------------------------

(define canvas (document.getElementById "canvas"))
(define ctx (canvas.getContext "2d"))

(define screen-width 1237)
(define screen-height 820)

(define load-time (js "new Date()"))

(set! ctx.fillStyle "#000")
(set! ctx.strokeStyle "#000")
